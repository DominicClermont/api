select DISTINCT * where {
   VALUES ?s {  <http://data.silknow.org/object/405d2aaf-49ea-3274-821b-bb2013493fb5> }

    ?s a ecrm:E22_Man-Made_Object;
        dc:identifier	?identifier .
    OPTIONAL {  ?s rdfs:label ?label }
    OPTIONAL {  ?s rdfs:comment ?comment }
    OPTIONAL {  ?s ecrm:P62_depicts ?picture }


    OPTIONAL {
     ?s ecrm:P138i_has_representation / schema:contentUrl ?img.
    }

    ?production ecrm:P108_has_produced ?s;
        ecrm:P126_employed ?material ;
        ecrm:P4_has_time-span	/ rdfs:label	?time .

    OPTIONAL {?production ecrm:P16_used_specific_object ?tool }
    OPTIONAL {?production ecrm:P8_took_place_on_or_within ?place }
    OPTIONAL {?production ecrm:P32_used_general_technique ?tech }

    [] ecrm:P104i_applies_to ?s;
        rdfs:comment ?right_comment ;
        ecrm:P75i_is_possessed_by	?right_owner .

    [] ecrm:P41_classified ?s;
        ecrm:P2_has_type "domain" ;
        ecrm:P42_assigned ?domain .

    [] ecrm:P41_classified ?s;
        ecrm:P2_has_type "denomination" ;
        ecrm:P42_assigned ?denomination .
}
